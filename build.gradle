plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group = 'net.frankheijden.serverutilsupdater'
project.ext {
    dependencyDir = group + '.dependencies'
}
version = '2.0.2'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

allprojects {
    ext {
        serverutilsVersion = '585eaab02a'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        maven { url 'https://repo.aikar.co/content/groups/aikar/' }
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        implementation "com.github.FrankHeijden.ServerUtils:ServerUtils-Common:${rootProject.serverutilsVersion}"
        testCompile 'org.assertj:assertj-core:3.15.0'
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.6.2'
        testCompile 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    }

    compileJava {
        options.compilerArgs += ["-parameters"]
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion "8.25"
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')

        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0
    }

    shadowJar.dependsOn checkstyleMain, checkstyleTest, test
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ':Common', configuration: 'shadow')
    implementation project(path: ':Bukkit', configuration: 'shadow')
    implementation project(path: ':Bungee', configuration: 'shadow')
    implementation project(path: ':Velocity', configuration: 'shadow')
}

build.dependsOn shadowJar

shadowJar {
    relocate 'net.frankheijden.serverutils.', dependencyDir + '.su.'
    relocate 'dev.frankheijden.minecraftreflection', dependencyDir + '.minecraftreflection'
    relocate 'cloud.commandframework', dependencyDir + '.cloud'
    relocate 'io.leangen.geantyref', dependencyDir + '.typetoken'
    relocate 'com.google.gson', dependencyDir + '.gson'
    exclude 'co/aikar/**'
    exclude 'org/bstats/**'
    exclude '*commands.json'
    exclude '*config.json'
    exclude '*messages.json'
}
