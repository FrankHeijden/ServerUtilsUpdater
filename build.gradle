plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'net.frankheijden.serverutilsupdater'
project.ext {
    dependencyDir = group + '.dependencies'
}
version = '3.0.2'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

allprojects {
    ext {
        serverutilsVersion = '3.3.0'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        maven { url 'https://repo.aikar.co/content/groups/aikar/' }
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'https://repo.fvdh.dev/snapshots' }
        maven { url 'https://repo.fvdh.dev/releases' }
    }

    dependencies {
        implementation "net.frankheijden.serverutils:ServerUtils-Common:${rootProject.serverutilsVersion}"
        testImplementation 'org.assertj:assertj-core:3.15.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    }

    compileJava {
        options.compilerArgs += ["-parameters"]
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion "8.25"
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')

        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0
    }

    shadowJar.dependsOn checkstyleMain, checkstyleTest, test
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation project(path: ':Common', configuration: 'shadow')
    implementation project(path: ':Bukkit', configuration: 'shadow')
    implementation project(path: ':Bungee', configuration: 'shadow')
    implementation project(path: ':Velocity', configuration: 'shadow')
}

build.dependsOn shadowJar

shadowJar {
    exclude '*commands.json'
    exclude '*config.json'
    exclude '*messages.json'
    exclude 'net/kyori/**'
    exclude 'cloud/commandframework/**'
    exclude 'com/google/**'
    exclude 'io/leangen/**'
    exclude 'javax/annotation/**'
    exclude 'org/bstats/**'
    exclude 'org/checkerframework/**'
    relocate 'net.frankheijden.serverutils.', dependencyDir + '.su.'
    relocate 'dev.frankheijden.minecraftreflection', dependencyDir + '.minecraftreflection'
    classifier ''
}

publishing {
    publications {
        shadow(MavenPublication) {
            artifact shadowJar
            artifactId "ServerUtilsUpdater"
        }
    }
}
